note
	description: "Main window for this application."
	author: "Generated by the Vision Application Wizard."
	date: "$Date: 2024/7/3 12:45:52 $"
	revision: "1.0.1"

class
	MAIN_WINDOW

inherit
	EV_TITLED_WINDOW
		redefine
			create_interface_objects,
			initialize,
			is_in_default_state
		end

create
	default_create

feature {NONE} -- Initialization

	create_interface_objects
			-- <Precursor>
		do
				-- Create main container.
			create main_container
			create_pixmap
			create_drawing_area
			new_game
		end

	initialize
			-- Build the interface for this window.
		do
			Precursor {EV_TITLED_WINDOW}

			drawing_area.expose_actions.extend (agent on_draw)
			drawing_area.resize_actions.extend (agent on_resize)

			build_main_container
			main_container.extend (drawing_area)

			extend (main_container)

				-- Execute `request_close_window' when the user clicks
				-- on the cross in the title bar.
			close_request_actions.extend (agent request_close_window)

			key_press_actions.extend (agent on_key_press)

				-- Set the title of the window.
			set_title (Window_title)

				-- Set the initial size of the window.
			set_size (Window_width, Window_height)
		end

	is_in_default_state: BOOLEAN
			-- Is the window in its default state?
			-- (as stated in `initialize')
		do
			Result := (width = Window_width) and then
				(height = Window_height) and then
				(title.is_equal (Window_title))
		end

feature {NONE} -- Implementation, Close event

	request_close_window
			-- Process user request to close the window.
		local
			question_dialog: EV_CONFIRMATION_DIALOG
		do
			create question_dialog.make_with_text (Label_confirm_close_window)
			question_dialog.show_modal_to_window (Current)

			if question_dialog.selected_button ~ (create {EV_DIALOG_CONSTANTS}).ev_ok then
					-- Destroy the window.
				destroy

					-- End the application.
					--| TODO: Remove next instruction if you don't want the application
					--|       to end when the first window is closed..
				if attached (create {EV_ENVIRONMENT}).application as a then
					a.destroy
				end
			end
		end

feature {NONE} -- Game state and timing

	last_update: TIME
			-- The time of the last simulation tick

	update_seconds: REAL = 0.1

	game_state: CHECKERBOARD
			-- the current game state

	new_game
		do
			create game_state.make
			create last_update.make_now
		end

	maybe_do_game_tick
		local
			now: TIME
		do
			create now.make_now

			if now.relative_duration (last_update).fine_seconds_count >= update_seconds then
				game_state.do_update
				last_update.fine_second_add (update_seconds)
			end

		end

feature {NONE} -- Drawing Area and Pixmap

	drawing_area: EV_DRAWING_AREA
			-- Area for custom drawing.

	pixmap: EV_PIXMAP
			-- Off-screen pixmap for double buffering.

	create_drawing_area
			-- Create and set up the drawing area.
		do
			create drawing_area
				-- drawing_area.set_background_color (create {EV_COLOR}.make_with_rgb (1, 1, 1))
		end

	create_pixmap
			-- Create and set up the pixmap for off-screen drawing.
		do
			create pixmap.make_with_size (Window_width, Window_height)
		end

	on_resize (x, y, w, h: INTEGER)
			-- Grow pixmap exponentially if required
		do
			if pixmap.width < w or pixmap.height < h then
				create pixmap.make_with_size (w.max (2 * pixmap.width), h.max (2 * pixmap.height))
			end
		ensure
			wide_enough: pixmap.width >= w
			tall_enough: pixmap.height >= h
		end

	on_draw (x, y, w, h: INTEGER)
			-- Handle paint event.
		require
			wide_enough: pixmap.width >= drawing_area.width
			tall_enough: pixmap.height >= drawing_area.height
		do
			maybe_do_game_tick

			pixmap.remove_clip_area
			pixmap.set_clip_area (create {EV_RECTANGLE}.make (0, 0, drawing_area.width, drawing_area.height))

			pixmap.set_background_color (create {EV_COLOR}.make_with_rgb (0.5, 0.5, 0.5))
			pixmap.clear

			game_state.draw (drawing_area.width, drawing_area.height, pixmap)

			drawing_area.draw_pixmap (0, 0, pixmap)
			drawing_area.redraw -- immediately request next repaint
		end

feature -- Event handling

	on_key_press (key: EV_KEY)
			-- Handle key press events.
		do

			if key.code = key.Key_a or key.code = key.Key_left then
				game_state.move_player (0)
			elseif key.code = key.Key_w or key.code = key.Key_up then
				game_state.move_player (1)
			elseif key.code = key.Key_s or key.code = key.Key_down then
				game_state.move_player (2)
			elseif key.code = key.Key_d or key.code = key.Key_right then
				game_state.move_player (3)
			end

				--game_state.move_player (a_key_event.key_code)

		end

feature {NONE} -- Implementation

	main_container: EV_VERTICAL_BOX
			-- Main container (contains all widgets displayed in this window).

	build_main_container
			-- Populate `main_container'.
		do
				-- main_container.extend (create {EV_TEXT}) -- Remove this line since it's replaced by drawing area
		ensure
			main_container_created: main_container /= Void
		end

feature {NONE} -- Implementation / Constants

	Window_title: STRING = "mice_and_cats_eiffel_gui"
			-- Title of the window.

	Window_width: INTEGER = 800
			-- Initial width for this window.

	Window_height: INTEGER = 800
			-- Initial height for this window.

	Label_confirm_close_window: STRING = "You are about to close this window.%NClick OK to proceed."
			-- String for the confirmation dialog box that appears
			-- when the user try to close the first window.

end
